#@TYPE: Machine
#@NAME: i.MX 8QXP ROM3620 A1
#@DESCRIPTION: i.MX 8QXP ROM-3620 board

MACHINEOVERRIDES =. "mx8:mx8x:mx8qxp:mx8qxpc0:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa35.inc
require conf/machine/include/advantech-imx8-settings.inc

MACHINE_FEATURES_append = " bcm43455 bcm4356 bcm4359"

# Don't include kernels in standard images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""
RDEPENDS_${KERNEL_PACKAGE_NAME}-image = ""

LOADADDR = ""

# Disable the serial console due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyAMA0"

# we do not want to have getty running on tty1 as we run
# auto-serial-console there
USE_VT = "0"

KERNEL_DEVICETREE = "freescale/imx8qxp-rom3620-a1.dtb \
			freescale/imx8qxp-rom3620-a1-lvds0-auo.dtb \
			freescale/imx8qxp-rom3620-a1-lvds1-auo.dtb \
			freescale/imx8qxp-rom3620-a1-hdmi-bridge.dtb \
			freescale/imx8qxp-rom3620-a1-lvds-chimei.dtb \
			freescale/imx8qxp-rom3620-a1-lvds-dual.dtb \
			freescale/imx8qxp-rom3620-a1-auog101uan02.dtb \
			freescale/recovery-imx8qxp-rom3620-a1.dtb "

UBOOT_MAKE_TARGET = \
    "${@bb.utils.contains('COMBINED_FEATURES', 'optee', '', \
                                                        'u-boot.bin', d)}"
SPL_BINARY = \
    "${@bb.utils.contains('COMBINED_FEATURES', 'optee', 'spl/u-boot-spl.bin', \
                                                        '', d)}"

UBOOT_MAKE_TARGET = "u-boot.bin"
UBOOT_SUFFIX = "bin"
UBOOT_CONFIG ??= "2G"
UBOOT_CONFIG[2G] = "imx8qxp_rom3620a1_2G_defconfig,sdcard"
UBOOT_CONFIG[FSPI_2G] = "imx8qxp_rom3620a1_2G_fspi_defconfig"

BOOT_SPACE = "65536"
IMAGE_BOOTLOADER = "imx-boot"
IMX_BOOT_SEEK = "32"

IMXBOOT_TARGETS_SD = \
    "${@bb.utils.contains('COMBINED_FEATURES', 'optee', 'flash_spl flash_linux_m4', \
                                                        'flash flash_regression_linux_m4', d)}"
IMXBOOT_TARGETS_FSPI = \
    "${@bb.utils.contains('COMBINED_FEATURES', 'optee', 'flash_spl_flexspi', \
                                                        'flash_flexspi', d)}"
IMXBOOT_TARGETS = \
    "${@bb.utils.contains_any('UBOOT_CONFIG', 'FSPI_2G', '${IMXBOOT_TARGETS_FSPI}', \
                                                '${IMXBOOT_TARGETS_SD}' , d)}"

BOARD_TYPE = "mek"

# Add additional firmware
MACHINE_FIRMWARE_append = " linux-firmware-ath10k"
